{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "578e1158",
   "metadata": {},
   "source": [
    "# Decision analysis with Julia `DecisionProgramming.jl` using Evans, 1997\n",
    "HOME: <https://di4health.github.io>\n",
    "\n",
    "Tomás Aragón, Updated 2026-01-21\n",
    "\n",
    "I mostly replicated R `rdecision` vignette \"Elementary decision tree (Evans 1997)\": https://cran.r-project.org/web/packages/rdecision/vignettes/DT01-Sumatriptan.html . \n",
    "\n",
    "Sources:\n",
    "- Briggs, Andrew Harvey, Karl Claxton, and Mark Sculpher. Decision Modelling for Health Economic Evaluation. 2006 (reprint 2207). Handbooks in Health Economic Evaluation Series. Oxford Univ. Press, 2006.\n",
    "- Evans, K. W., J. A. Boan, J. L. Evans, and A. Shuaib. “Economic Evaluation of Oral Sumatriptan Compared with Oral Caffeine/Ergotamine for Migraine.” PharmacoEconomics 12, no. 5 (1997): 565–77. https://doi.org/10.2165/00019053-199712050-00007.\n",
    "\n",
    "This Julia Jupyter notebook was created in VS Code and contains examples from Evans, 1997. You can run this notebook in VS Code, Positron, or JupyterLab with a Julia kernel. To learn about the Julia language visit https://julialang.org/ .\n",
    "\n",
    "We will use `DecisionProgramming.jl` Julia package: https://gamma-opt.github.io/DecisionProgramming.jl/dev/ . `DecisionProgramming.jl` is a Julia package for modeling and solving decision analysis problems structured as influence diagrams and solved using mixed-integer linear programming.\n",
    "\n",
    "Preparation: \n",
    "- Review [R Jupyter Notebook of decision analysis of Evans 1997 using `rdecision`](https://github.com/di4health/di4h/blob/main/nb/Evans1997/NB_R_rdecision_Evans1997.ipynb). - We will replicate this R analysis in Julia. \n",
    "- Review `DecisionProgramming.jl` GitHub page for introduction to syntax: https://github.com/gamma-opt/DecisionProgramming.jl . \n",
    "  - Documentation is here: https://gamma-opt.github.io/DecisionProgramming.jl/dev/ \n",
    "\n",
    "\n",
    "Optional readings for background:\n",
    "- Owens, Douglas K. “Analytic Tools for Public Health Decision Making.” Medical Decision Making: An International Journal of the Society for Medical Decision Making 22, no. 5 Suppl (2002): S3-10. https://doi.org/10.1177/027298902237969.\n",
    "- Owens, D. K., R. D. Shachter, and R. F. Nease. “Representation and Analysis of Medical Decision Problems with Influence Diagrams.” Medical Decision Making: An International Journal of the Society for Medical Decision Making 17, no. 3 (1997): 241–62. https://doi.org/10.1177/0272989X9701700301.\n",
    "- Nease, R. F., and D. K. Owens. “Use of Influence Diagrams to Structure Medical Decisions.” Medical Decision Making: An International Journal of the Society for Medical Decision Making 17, no. 3 (1997): 263–75. https://doi.org/10.1177/0272989X9701700302.\n",
    "- Neapolitan, Richard, Xia Jiang, Daniela P. Ladner, and Bruce Kaplan. “A Primer on Bayesian Decision Analysis With an Application to a Kidney Transplant Decision.” Transplantation 100, no. 3 (2016): 489–96. https://doi.org/10.1097/TP.0000000000001145."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5974fa59",
   "metadata": {},
   "source": [
    "<figure>\n",
    "<img src=\"img_Briggs2006_Box2-3.png\" width=\"700\" alt=\"Evans 1997 Figure 1\"/>\n",
    "<figcaption>FIGURE 1: BOX 2.3. Decision tree from Briggs et al., 2006.</figcaption>\n",
    "</figure>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d32f0064",
   "metadata": {},
   "source": [
    "Figure 1 is the Table 1 from Evans, 1997, as depicted in Briggs, 2006. Figure 2 depicts how I will label the nodes and their states (outcomes). For example, R = relief node with states {R1, R2}. I am using \"R1\" and \"R2\" instead of 1 and 2. This makes it easier to keep track of the node states in coding. "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9066b46b",
   "metadata": {},
   "source": [
    "<figure>\n",
    "<img src=\"img_evans1997_fig01.drawio.png\" width=\"900\" alt=\"Evans 1997 Figure 1\"/>\n",
    "<figcaption>FIGURE 2: From Evans, 1997, Figure 1.</figcaption>\n",
    "</figure>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bf787a8d",
   "metadata": {},
   "source": [
    "Figure 3 is the influence diagram equivalent of Figure 2. $U$ is the utility value node which collects the final outcomes which will be weighted by their probabilities to calculate expected utililty. $C_i$ are the cost value nodes which will be weighted by their probabilities to calculate expected cost."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2de0ce7a",
   "metadata": {},
   "source": [
    "<figure>\n",
    "<img src=\"img_evans1997_Influence_Diagram_V2.drawio.png\" width=\"700\" alt=\"Evans 1997 Figure 1\"/>\n",
    "<figcaption>FIGURE 3. Influence diagram derived from Evans, 1997, Figure 1.</figcaption>\n",
    "</figure>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1de0b4a7",
   "metadata": {},
   "source": [
    "`DecisionProgramming.jl` deploy mixed-integer linear programming and can only optimize one utility type at a time. So our strategy is the following.\n",
    "\n",
    "1. Construct influence diagram\n",
    "2. Add value nodes (eg, with utilities)\n",
    "3. Run opitmization for expected value\n",
    "4. Repeat steps 1 to 3 but with another set of value nodes (eg, cost).\n",
    "\n",
    "It's easier to create functions and then reuse them. \n",
    "\n",
    "Because the paths are asymmetric, we need to include states that are \"not available\" (NA) when another path has been realized. This occurs with nodes N, E, and H."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "33c02d35",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Running HiGHS 1.12.0 (git hash: 755a8e027): Copyright (c) 2025 HiGHS under MIT licence terms\n",
      "MIP has 189 rows; 147 cols; 579 nonzeros; 2 integer variables (2 binary)\n",
      "Coefficient ranges:\n",
      "  Matrix  [2e-03, 1e+00]\n",
      "  Cost    [1e-01, 1e+00]\n",
      "  Bound   [1e+00, 1e+00]\n",
      "  RHS     [1e+00, 1e+00]\n",
      "Presolving model\n",
      "24 rows, 20 cols, 58 nonzeros  0s\n",
      "0 rows, 0 cols, 0 nonzeros  0s\n",
      "Presolve reductions: rows 0(-189); columns 0(-147); nonzeros 0(-579) - Reduced to empty\n",
      "Presolve: Optimal\n",
      "\n",
      "Src: B => Branching; C => Central rounding; F => Feasibility pump; H => Heuristic;\n",
      "     I => Shifting; J => Feasibility jump; L => Sub-MIP; P => Empty MIP; R => Randomized rounding;\n",
      "     S => Solve LP; T => Evaluate node; U => Unbounded; X => User solution; Y => HiGHS solution;\n",
      "     Z => ZI Round; l => Trivial lower; p => Trivial point; u => Trivial upper; z => Trivial zero\n",
      "\n",
      "        Nodes      |    B&B Tree     |            Objective Bounds              |  Dynamic Constraints |       Work      \n",
      "Src  Proc. InQueue |  Leaves   Expl. | BestBound       BestSol              Gap |   Cuts   InLp Confl. | LpIters     Time\n",
      "\n",
      "         0       0         0   0.00%   0.4168609018    0.4168609018       0.00%        0      0      0         0     0.0s\n",
      "\n",
      "Solving report\n",
      "  Status            Optimal\n",
      "  Primal bound      0.416860901803\n",
      "  Dual bound        0.416860901803\n",
      "  Gap               0% (tolerance: 0.01%)\n",
      "  P-D integral      0\n",
      "  Solution status   feasible\n",
      "                    0.416860901803 (objective)\n",
      "                    0 (bound viol.)\n",
      "                    0 (int. viol.)\n",
      "                    0 (row viol.)\n",
      "  Timing            0.00\n",
      "  Max sub-MIP depth 0\n",
      "  Nodes             0\n",
      "  Repair LPs        0\n",
      "  LP iterations     0\n",
      "Running HiGHS 1.12.0 (git hash: 755a8e027): Copyright (c) 2025 HiGHS under MIT licence terms\n",
      "MIP has 189 rows; 147 cols; 579 nonzeros; 2 integer variables (2 binary)\n",
      "Coefficient ranges:\n",
      "  Matrix  [2e-03, 1e+00]\n",
      "  Cost    [1e-01, 1e+00]\n",
      "  Bound   [1e+00, 1e+00]\n",
      "  RHS     [1e+00, 1e+00]\n",
      "Presolving model\n",
      "24 rows, 20 cols, 58 nonzeros  0s\n",
      "0 rows, 0 cols, 0 nonzeros  0s\n",
      "Presolve reductions: rows 0(-189); columns 0(-147); nonzeros 0(-579) - Reduced to empty\n",
      "Presolve: Optimal\n",
      "\n",
      "Src: B => Branching; C => Central rounding; F => Feasibility pump; H => Heuristic;\n",
      "     I => Shifting; J => Feasibility jump; L => Sub-MIP; P => Empty MIP; R => Randomized rounding;\n",
      "     S => Solve LP; T => Evaluate node; U => Unbounded; X => User solution; Y => HiGHS solution;\n",
      "     Z => ZI Round; l => Trivial lower; p => Trivial point; u => Trivial upper; z => Trivial zero\n",
      "\n",
      "        Nodes      |    B&B Tree     |            Objective Bounds              |  Dynamic Constraints |       Work      \n",
      "Src  Proc. InQueue |  Leaves   Expl. | BestBound       BestSol              Gap |   Cuts   InLp Confl. | LpIters     Time\n",
      "\n",
      "         0       0         0   0.00%   0.2012759466    0.2012759466       0.00%        0      0      0         0     0.0s\n",
      "\n",
      "Solving report\n",
      "  Status            Optimal\n",
      "  Primal bound      0.201275946578\n",
      "  Dual bound        0.201275946578\n",
      "  Gap               0% (tolerance: 0.01%)\n",
      "  P-D integral      0\n",
      "  Solution status   feasible\n",
      "                    0.201275946578 (objective)\n",
      "                    0 (bound viol.)\n",
      "                    0 (int. viol.)\n",
      "                    0 (row viol.)\n",
      "  Timing            0.00\n",
      "  Max sub-MIP depth 0\n",
      "  Nodes             0\n",
      "  Repair LPs        0\n",
      "  LP iterations     0\n",
      "Running HiGHS 1.12.0 (git hash: 755a8e027): Copyright (c) 2025 HiGHS under MIT licence terms\n",
      "MIP has 64 rows; 47 cols; 181 nonzeros; 2 integer variables (2 binary)\n",
      "Coefficient ranges:\n",
      "  Matrix  [2e-03, 1e+00]\n",
      "  Cost    [1e+00, 1e+03]\n",
      "  Bound   [1e+00, 1e+00]\n",
      "  RHS     [1e+00, 1e+00]\n",
      "Presolving model\n",
      "15 rows, 11 cols, 33 nonzeros  0s\n",
      "0 rows, 0 cols, 0 nonzeros  0s\n",
      "Presolve reductions: rows 0(-64); columns 0(-47); nonzeros 0(-181) - Reduced to empty\n",
      "Presolve: Optimal\n",
      "\n",
      "Src: B => Branching; C => Central rounding; F => Feasibility pump; H => Heuristic;\n",
      "     I => Shifting; J => Feasibility jump; L => Sub-MIP; P => Empty MIP; R => Randomized rounding;\n",
      "     S => Solve LP; T => Evaluate node; U => Unbounded; X => User solution; Y => HiGHS solution;\n",
      "     Z => ZI Round; l => Trivial lower; p => Trivial point; u => Trivial upper; z => Trivial zero\n",
      "\n",
      "        Nodes      |    B&B Tree     |            Objective Bounds              |  Dynamic Constraints |       Work      \n",
      "Src  Proc. InQueue |  Leaves   Expl. | BestBound       BestSol              Gap |   Cuts   InLp Confl. | LpIters     Time\n",
      "\n",
      "         0       0         0   0.00%   22.05805782     22.05805782        0.00%        0      0      0         0     0.0s\n",
      "\n",
      "Solving report\n",
      "  Status            Optimal\n",
      "  Primal bound      22.0580578225\n",
      "  Dual bound        22.0580578225\n",
      "  Gap               0% (tolerance: 0.01%)\n",
      "  P-D integral      0\n",
      "  Solution status   feasible\n",
      "                    22.0580578225 (objective)\n",
      "                    0 (bound viol.)\n",
      "                    0 (int. viol.)\n",
      "                    0 (row viol.)\n",
      "  Timing            0.00\n",
      "  Max sub-MIP depth 0\n",
      "  Nodes             0\n",
      "  Repair LPs        0\n",
      "  LP iterations     0\n",
      "Running HiGHS 1.12.0 (git hash: 755a8e027): Copyright (c) 2025 HiGHS under MIT licence terms\n",
      "MIP has 64 rows; 47 cols; 181 nonzeros; 2 integer variables (2 binary)\n",
      "Coefficient ranges:\n",
      "  Matrix  [2e-03, 1e+00]\n",
      "  Cost    [1e+00, 1e+03]\n",
      "  Bound   [1e+00, 1e+00]\n",
      "  RHS     [1e+00, 1e+00]\n",
      "Presolving model\n",
      "15 rows, 11 cols, 33 nonzeros  0s\n",
      "0 rows, 0 cols, 0 nonzeros  0s\n",
      "Presolve reductions: rows 0(-64); columns 0(-47); nonzeros 0(-181) - Reduced to empty\n",
      "Presolve: Optimal\n",
      "\n",
      "Src: B => Branching; C => Central rounding; F => Feasibility pump; H => Heuristic;\n",
      "     I => Shifting; J => Feasibility jump; L => Sub-MIP; P => Empty MIP; R => Randomized rounding;\n",
      "     S => Solve LP; T => Evaluate node; U => Unbounded; X => User solution; Y => HiGHS solution;\n",
      "     Z => ZI Round; l => Trivial lower; p => Trivial point; u => Trivial upper; z => Trivial zero\n",
      "\n",
      "        Nodes      |    B&B Tree     |            Objective Bounds              |  Dynamic Constraints |       Work      \n",
      "Src  Proc. InQueue |  Leaves   Expl. | BestBound       BestSol              Gap |   Cuts   InLp Confl. | LpIters     Time\n",
      "\n",
      "         0       0         0   0.00%   4.714972491     4.714972491        0.00%        0      0      0         0     0.0s\n",
      "\n",
      "Solving report\n",
      "  Status            Optimal\n",
      "  Primal bound      4.71497249078\n",
      "  Dual bound        4.71497249078\n",
      "  Gap               0% (tolerance: 0.01%)\n",
      "  P-D integral      0\n",
      "  Solution status   feasible\n",
      "                    4.71497249078 (objective)\n",
      "                    0 (bound viol.)\n",
      "                    0 (int. viol.)\n",
      "                    0 (row viol.)\n",
      "  Timing            0.00\n",
      "  Max sub-MIP depth 0\n",
      "  Nodes             0\n",
      "  Repair LPs        0\n",
      "  LP iterations     0\n",
      "=== Cost-Effectiveness Analysis ===\n",
      "Strategy         E[Cost]     E[Utility]\n",
      "Caffeine/Ergot   4.715    0.2013\n",
      "Sumatriptan      22.0581   0.4169\n",
      "\n",
      "Incremental Cost:    17.3431\n",
      "Incremental Utility: 0.2156\n",
      "ICER: $29363.0 Can/QALY\n"
     ]
    }
   ],
   "source": [
    "using JuMP, HiGHS\n",
    "using DecisionProgramming\n",
    "\n",
    "# ============================================================\n",
    "# Helper function: builds the chance node structure (shared)\n",
    "# ============================================================\n",
    "function build_chance_structure!(diagram)\n",
    "    add_node!(diagram, DecisionNode(\"D\", [], [\"D1\", \"D2\"]))\n",
    "    add_node!(diagram, ChanceNode(\"R\", [\"D\"], [\"R1\", \"R2\"]))\n",
    "    add_node!(diagram, ChanceNode(\"N\", [\"D\", \"R\"], [\"N1\", \"N2\", \"NA\"])) # Include \"NA\" for case where R = R2 (no relief);  P(N = \"NA\" | R = \"R2\") = 1\n",
    "    add_node!(diagram, ChanceNode(\"E\", [\"R\"], [\"E1\", \"E2\", \"NA\"])) # Include \"NA\" for case where R = R1 (yes relief); P(E = \"NA\" | R = \"R1\") = 1\n",
    "    add_node!(diagram, ChanceNode(\"H\", [\"E\"], [\"H1\", \"H2\", \"NA\"])) # Include \"NA\" for case where E = E1 (endures attack); P(H = \"NA\" | E = \"E1\") = 1\n",
    "end\n",
    "\n",
    "function add_chance_probabilities!(diagram)\n",
    "    # P(R | D)\n",
    "    X_R = ProbabilityMatrix(diagram, \"R\")\n",
    "    X_R[\"D1\", \"R1\"] = 0.558 # P(R1 | D1) = 0.558\n",
    "    X_R[\"D1\", \"R2\"] = 0.442 # 1 - P(R1 | D1) = 0.442\n",
    "    X_R[\"D2\", \"R1\"] = 0.379 # P(R1 | D2) = 0.379\n",
    "    X_R[\"D2\", \"R2\"] = 0.621 # 1 - P(R1 | D2) = 0.621\n",
    "    add_probabilities!(diagram, \"R\", X_R)\n",
    "\n",
    "    # P(N | D, R)\n",
    "    X_N = ProbabilityMatrix(diagram, \"N\")\n",
    "    X_N[\"D1\", \"R1\", \"N1\"] = 0.594 # P(N1 | D1, R1) = 0.594\n",
    "    X_N[\"D1\", \"R1\", \"N2\"] = 0.406 # 1 - P(N1 | D1, R1) = 0.406\n",
    "    X_N[\"D1\", \"R1\", \"NA\"] = 0.0 # P(NA | D1, R1) = 0   \n",
    "    X_N[\"D1\", \"R2\", \"N1\"] = 0.0 # P(N1 | D1, R2) = 0 \n",
    "    X_N[\"D1\", \"R2\", \"N2\"] = 0.0 # P(N2 | D1, R2) = 0 \n",
    "    X_N[\"D1\", \"R2\", \"NA\"] = 1.0 # P(NA | D1, R2) = 1\n",
    "    X_N[\"D2\", \"R1\", \"N1\"] = 0.703 # P(N1 | D2, R1) = 0.703\n",
    "    X_N[\"D2\", \"R1\", \"N2\"] = 0.297 # 1 - P(N1 | D2, R1) = 0.297\n",
    "    X_N[\"D2\", \"R1\", \"NA\"] = 0.0 # P(NA | D2, R1) = 0\n",
    "    X_N[\"D2\", \"R2\", \"N1\"] = 0.0 # P(N1 | D2, R2) = 0\n",
    "    X_N[\"D2\", \"R2\", \"N2\"] = 0.0 # P(N2 | D2, R2) = 0\n",
    "    X_N[\"D2\", \"R2\", \"NA\"] = 1.0 # P(NA | D2, R2) = 1\n",
    "    add_probabilities!(diagram, \"N\", X_N)\n",
    "\n",
    "    # P(E | R)\n",
    "    X_E = ProbabilityMatrix(diagram, \"E\")\n",
    "    X_E[\"R1\", \"E1\"] = 0.0 # P(E1 | R1) = 0\n",
    "    X_E[\"R1\", \"E2\"] = 0.0 # P(E2 | R1) = 0 \n",
    "    X_E[\"R1\", \"NA\"] = 1.0 # P(NA | R1) = 1\n",
    "    X_E[\"R2\", \"E1\"] = 0.92 # P(E1 | R2) = 0.92 \n",
    "    X_E[\"R2\", \"E2\"] = 0.08 # 1 - P(E1 | R2) = 0.08\n",
    "    X_E[\"R2\", \"NA\"] = 0.0 # P(NA | R2) = 0\n",
    "    add_probabilities!(diagram, \"E\", X_E)\n",
    "\n",
    "    # P(H | E)\n",
    "    X_H = ProbabilityMatrix(diagram, \"H\")\n",
    "    X_H[\"E1\", \"H1\"] = 0.0 # P(H1 | E1) = 0\n",
    "    X_H[\"E1\", \"H2\"] = 0.0 # P(H2 | E1) = 0\n",
    "    X_H[\"E1\", \"NA\"] = 1.0 # P(NA | E1) = 1\n",
    "    X_H[\"E2\", \"H1\"] = 0.998 # P(H1 | E2) = 0.998\n",
    "    X_H[\"E2\", \"H2\"] = 0.002 # 1 - P(H1 | E2) = 0.002\n",
    "    X_H[\"E2\", \"NA\"] = 0.0 # P(NA | E2) = 0\n",
    "    X_H[\"NA\", \"H1\"] = 0.0 # P(H1 | NA) = 0\n",
    "    X_H[\"NA\", \"H2\"] = 0.0 # P(H2 | NA) = 0\n",
    "    X_H[\"NA\", \"NA\"] = 1.0 # P(NA | NA) = 1\n",
    "    add_probabilities!(diagram, \"H\", X_H)\n",
    "end\n",
    "\n",
    "# ============================================================\n",
    "# DIAGRAM 1: Utility only\n",
    "# ============================================================\n",
    "diag_u = InfluenceDiagram()\n",
    "build_chance_structure!(diag_u)\n",
    "add_node!(diag_u, ValueNode(\"U\", [\"D\", \"N\", \"E\", \"H\"]))\n",
    "generate_arcs!(diag_u)\n",
    "add_chance_probabilities!(diag_u)\n",
    "\n",
    "Y_U = UtilityMatrix(diag_u, \"U\")\n",
    "Y_U .= 0.0    # set default utility to 0.0 for all states (including unreachable ones)\n",
    "# Only assign reachable states (all others default to 0.0)\n",
    "Y_U[\"D1\", \"N1\", \"NA\", \"NA\"] =  1.0    # A: relief, no recurrence\n",
    "Y_U[\"D1\", \"N2\", \"NA\", \"NA\"] =  0.9    # B: relief, recurrence\n",
    "Y_U[\"D1\", \"NA\", \"E1\", \"NA\"] = -0.3    # C: no relief, endures\n",
    "Y_U[\"D1\", \"NA\", \"E2\", \"H1\"] =  0.1    # D: no relief, ED relief\n",
    "Y_U[\"D1\", \"NA\", \"E2\", \"H2\"] = -0.3    # E: no relief, hospitalized\n",
    "Y_U[\"D2\", \"N1\", \"NA\", \"NA\"] =  1.0    # F\n",
    "Y_U[\"D2\", \"N2\", \"NA\", \"NA\"] =  0.9    # G\n",
    "Y_U[\"D2\", \"NA\", \"E1\", \"NA\"] = -0.3    # H\n",
    "Y_U[\"D2\", \"NA\", \"E2\", \"H1\"] =  0.1    # I\n",
    "Y_U[\"D2\", \"NA\", \"E2\", \"H2\"] = -0.3    # J\n",
    "add_utilities!(diag_u, \"U\", Y_U)\n",
    "\n",
    "# ============================================================\n",
    "# DIAGRAM 2: Cost only (C1 + C2 + C3 + C4)\n",
    "# ============================================================\n",
    "diag_c = InfluenceDiagram()\n",
    "build_chance_structure!(diag_c)\n",
    "add_node!(diag_c, ValueNode(\"C1\", [\"D\"]))\n",
    "add_node!(diag_c, ValueNode(\"C2\", [\"D\", \"N\"]))\n",
    "add_node!(diag_c, ValueNode(\"C3\", [\"E\"]))\n",
    "add_node!(diag_c, ValueNode(\"C4\", [\"H\"]))\n",
    "generate_arcs!(diag_c)\n",
    "add_chance_probabilities!(diag_c)\n",
    "\n",
    "Y_C1 = UtilityMatrix(diag_c, \"C1\")\n",
    "Y_C1[\"D1\"] = 16.10\n",
    "Y_C1[\"D2\"] = 1.32\n",
    "\n",
    "Y_C2 = UtilityMatrix(diag_c, \"C2\")\n",
    "Y_C2[\"D1\", \"N1\"] = 0.0\n",
    "Y_C2[\"D1\", \"N2\"] = 16.10\n",
    "Y_C2[\"D1\", \"NA\"] = 0.0\n",
    "Y_C2[\"D2\", \"N1\"] = 0.0\n",
    "Y_C2[\"D2\", \"N2\"] = 1.32\n",
    "Y_C2[\"D2\", \"NA\"] = 0.0\n",
    "\n",
    "Y_C3 = UtilityMatrix(diag_c, \"C3\")\n",
    "Y_C3[\"E1\"] = 0.0\n",
    "Y_C3[\"E2\"] = 63.16\n",
    "Y_C3[\"NA\"] = 0.0\n",
    "\n",
    "Y_C4 = UtilityMatrix(diag_c, \"C4\")\n",
    "Y_C4[\"H1\"] = 0.0\n",
    "Y_C4[\"H2\"] = 1093.0\n",
    "Y_C4[\"NA\"] = 0.0\n",
    "\n",
    "add_utilities!(diag_c, \"C1\", Y_C1)\n",
    "add_utilities!(diag_c, \"C2\", Y_C2)\n",
    "add_utilities!(diag_c, \"C3\", Y_C3)\n",
    "add_utilities!(diag_c, \"C4\", Y_C4)\n",
    "\n",
    "# ============================================================\n",
    "# Evaluate each strategy\n",
    "# ============================================================\n",
    "optimizer = optimizer_with_attributes(() -> HiGHS.Optimizer())\n",
    "\n",
    "function evaluate_strategy(diagram, decision_state::Int)\n",
    "    model, z, variables = generate_model(diagram, model_type=\"RJT\")\n",
    "    d_vars = z[\"D\"].z    # access the underlying VariableRef array\n",
    "    for i in 1:2\n",
    "        @constraint(model, d_vars[i] == (i == decision_state ? 1 : 0))\n",
    "    end\n",
    "    set_optimizer(model, optimizer)\n",
    "    optimize!(model)\n",
    "    Z = DecisionStrategy(diagram, z)\n",
    "    S_prob = StateProbabilities(diagram, Z)\n",
    "    U_dist = UtilityDistribution(diagram, Z)\n",
    "    return Z, S_prob, U_dist\n",
    "end\n",
    "\n",
    "# --- Expected Utility per strategy ---\n",
    "_, _, U_dist_D1 = evaluate_strategy(diag_u, 1)\n",
    "_, _, U_dist_D2 = evaluate_strategy(diag_u, 2)\n",
    "EU_D1 = U_dist_D1.u' * U_dist_D1.p    # or use print_statistics\n",
    "EU_D2 = U_dist_D2.u' * U_dist_D2.p\n",
    "\n",
    "# --- Expected Cost per strategy ---\n",
    "_, _, C_dist_D1 = evaluate_strategy(diag_c, 1)\n",
    "_, _, C_dist_D2 = evaluate_strategy(diag_c, 2)\n",
    "EC_D1 = C_dist_D1.u' * C_dist_D1.p\n",
    "EC_D2 = C_dist_D2.u' * C_dist_D2.p\n",
    "\n",
    "# ============================================================\n",
    "# Cost-Effectiveness Analysis\n",
    "# ============================================================\n",
    "println(\"=== Cost-Effectiveness Analysis ===\")\n",
    "println(\"Strategy         E[Cost]     E[Utility]\")\n",
    "println(\"Caffeine/Ergot   $(round(EC_D2, digits=4))    $(round(EU_D2, digits=4))\")\n",
    "println(\"Sumatriptan      $(round(EC_D1, digits=4))   $(round(EU_D1, digits=4))\")\n",
    "println()\n",
    "\n",
    "ΔC = EC_D1 - EC_D2\n",
    "ΔU = EU_D1 - EU_D2\n",
    "println(\"Incremental Cost:    $(round(ΔC, digits=4))\")\n",
    "println(\"Incremental Utility: $(round(ΔU, digits=4))\")\n",
    "\n",
    "# ICER per QALY (24-hour time horizon → annualize by multiplying by 365)\n",
    "ICER_per_QALY = (ΔC / ΔU) * 365\n",
    "println(\"ICER: \\$$(round(ICER_per_QALY, digits=0)) Can/QALY\")\n",
    "\n",
    "# Expected R reference values:\n",
    "# E[C|Caffeine]    = 4.7150,  E[U|Caffeine]    = 0.2013\n",
    "# E[C|Sumatriptan] = 22.0581, E[U|Sumatriptan] = 0.4169\n",
    "# ICER ≈ $29,363 Can/QALY"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "233b3473",
   "metadata": {},
   "source": [
    "## Results from this notebook (`DecisionProgramming.jl`)\n",
    "\n",
    "```julia\n",
    "=== Cost-Effectiveness Analysis ===\n",
    "Strategy         E[Cost]     E[Utility]\n",
    "Caffeine/Ergot    4.715      0.2013\n",
    "Sumatriptan      22.0581     0.4169\n",
    "\n",
    "Incremental Cost:    17.3431\n",
    "Incremental Utility: 0.2156\n",
    "ICER: $29363.0 Can/QALY\n",
    "```\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0906600d",
   "metadata": {},
   "source": [
    "## Results from `rdecision` (NB_R_rdecision_Evans1997.ipynb)\n",
    "<https://github.com/di4health/di4h/blob/main/nb/Evans1997/NB_R_rdecision_Evans1997.ipynb>\n",
    "\n",
    "```julia\n",
    "Run Migraine.... TREATMENT ..d01. Probability      Cost Benefit   Utility          QALY\n",
    "1   1        Caffeine/Ergot (e02)           1  4.714972       0 0.2012760  0.0005510635\n",
    "2   1           Sumatriptan (e01)           1 22.058057       0 0.4168609  0.0011413030\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "30cf18d4",
   "metadata": {},
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.3",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
